// Support for timestamp format <43>Feb  4 08:00:24
[RULE:NEW_FORMAT_TWO_STEPS]

// extract timestamp from the log, located inside and not the beginning
alter tmp_extract_timestamp = arrayindex(regextract(_raw_log , "\s\[?(\d{4}-\d{2}-\d{2}T\d{2}:\d{2}:\d{2}.\d+)Z\]?\s"), 0)
// since the time format including T and Z, inserting hardcoded 00:00 
| alter tmp_new_timestamp = concat(" ", tmp_extract_timestamp , "+00:00 ")
// replacing the first timestamp instance with the second instance. the prival maintained the same
| alter _raw_log = replex(_raw_log , "\w{3}\s+\d{1,2}\s+\d{2}:\d{2}:\d{2}\s+", tmp_new_timestamp )
|fields -tmp_extract_timestamp ,tmp_new_timestamp ;


// Support only date time of format: yyyy-MM-ddThh:mm:ss.nnnnnn[+|-]nn:nn. For example: 2022-01-01T10:00:00.012345+00:00
[RULE:OLD_FORMAT_ONE_STEP]

alter
    tmp_date_part = arraystring(regextract(_raw_log, "^[a-zA-Z0-9\W]{0,10}\s(\d{4}\-\d{2}\-\d{2})T"), ""),
    tmp_time_part = arraystring(regextract(_raw_log, "^[a-zA-Z0-9\W]{0,10}\s\d{4}\-\d{2}\-\d{2}T(\d{2}\:\d{2}\:\d{2}\.)"), ""),
    tmp_mili_part = arraystring(regextract(_raw_log, "^[a-zA-Z0-9\W]{0,10}\s\d{4}\-\d{2}\-\d{2}T\d{2}\:\d{2}\:\d{2}\.(\d+)"), ""),
    tmp_zone_part = arraystring(regextract(_raw_log, "^[a-zA-Z0-9\W]{0,10}\s\d{4}\-\d{2}\-\d{2}T\d{2}\:\d{2}\:\d{2}\.\d+([+|-]\d+:\d+)"), "")
| alter 
    tmp_time_format = arraystring(arraycreate(tmp_date_part, tmp_time_part), " "),
    tmp_milli_zone = arraystring(arraycreate(tmp_mili_part, tmp_zone_part), "")
| alter
    tmp_adjust_tmp_milli_zone = replex(tmp_milli_zone, ":", "")
| alter
    tmp_timestamp = arraystring(arraycreate(tmp_time_format, tmp_adjust_tmp_milli_zone), "")
| alter
    _time = parse_timestamp("%Y-%m-%d %H:%M:%E*S%z", tmp_timestamp)
| fields -tmp_date_part, tmp_time_part, tmp_mili_part, tmp_zone_part, tmp_time_format, tmp_milli_zone, tmp_adjust_tmp_milli_zone, tmp_timestamp;


[INGEST:vendor="vmware", product="vcenter", target_dataset="vmware_vcenter_raw", no_hit=keep]
filter _raw_log ~= "<\d{1,3}>\w{3}\s+\d{1,2}\s+\d{2}:\d{2}:\d{2}\s+"
|call NEW_FORMAT_TWO_STEPS 
|call OLD_FORMAT_ONE_STEP;

filter _raw_log ~= "^[a-zA-Z0-9\W]{0,10}\s\d{4}\-\d{2}\-\d{2}T\d{2}\:\d{2}\:\d{2}\.\d+[+|-]\d+:\d+"
|call OLD_FORMAT_ONE_STEP;